<!-- markdownlint-disable MD033 -->
<!-- markdownlint-disable MD010 -->
<!-- markdownlint-disable MD029 -->
<!-- markdownlint-disable MD037 -->

# 3. 如何準備 SAS 資料

## 創建資料

## 基本創建方法

```SAS
DATA data_name;
    INPUT feature(s); /* 在 feature 後面加上 $ 才能輸入 str, 不然內建為數值型*/
    DATALINES; /* 開始輸入資料, 也可以打 CARDS 或 INSTREAM */
    your data
    ;
run;
```

其中, CARDS 和 INSTREAM 跟 DATALINES 的使用方法差不多, 若有差異的用法之後會註記. 較長使用 DATALINES.

### 遺漏值資料

若資料中有遺漏值, 可以使用 MISSING 宣告, 或是使用 "." 代替(若為 str 可以直接空白, 不過建議不要.). 並且雖然有輸入但若不想顯示有遺漏值的項, 可以搭配 IF 使用.

```SAS
DATA miss1;
    MISSING a b;
    INPUT name $ income;
    IF income in (., .a, .b) THEN DELETE; /* 註 : 這邊書上是使用 OR 進行編寫(p3-16) */
DATALINES;
    Alice   .
    Afred   10000
    Bob     a
    Dan     50000
    Penny   b
;
RUN;

PROC PRINT DATA = miss1; 
RUN;
```

![3-1](圖片\3-1.PNG)

# 4. 如何將原始資料轉成 SAS 資料檔

## 4.2 分析 SAS 程式外的資料

* PRINT : 用來印出 DATA
* MEANS : 用來簡單分析 DATA, 輸出為 非遺漏值個數、平均數、標準差、最小值、最大值. 用於連續型
* FREQ : 計數. 用於離散型

建議在分析資料前, 如果可以的話多使用以上三個 PROC, 以確保資料或編碼沒有疏失. 當然如果 TABLE 太大可能會導致 PRINT 花很久時間, 這邊就自己斟酌.

```SAS
DATA eval;
	INFILE 'D:\github\git_data\SAS\SAS123\DATA\Y777.dat'; /* 讀取外部資料 */
	INPUT id $ a1-a5 b1-b5 q16-q19 q20a $; /* 建立 feature, 若是文字變項則要在後面加上 $ */
RUN;

PROC PRINT DATA = eval; /* 印出 eval */
RUN;

PROC MEANS DATA = eval; /* 計算 eval 的每個 col 的 N, mean, sd, min, max */
RUN;

PROC FREQ DATA = eval;
	TABLES q20a; /* 對 eval 的 q20a 進行計數 */
RUN;
```

## 4.3 如何應用 INPUT 指令讀入資料

INPUT 的注意事項 :

### 簡列輸入法(List Input)

例 :

```SAS
INPUT name $ sex $ age height weight;
```

輸入 :

```SAS
mei          f .  68 100
```

* 變項值依照變項的排列次序一一登入
* 變項值長度最多八字元, 可透過 LENGTH 指令更改長度, 例如 : LENGTH firstname $ 12; 此把 firstname 這個變項的值字元數增加到12.
* $ 符號前的變項代表文字變項
* 連續的便項名只需寫出最前和最後兩個變項名, 中間再以 - 連結.

輸入值的注意事項

* 輸入的各個資料間至少要有一個空格(可多個), 文字變項若資料本身有空格則要改成其他字(例如 : _ )
* 遺漏值以 "." 表示
* 若希望文字變項本身的空格合法, 則在 INPUT 時, \$ 後要加上 &. <font size = 3 color = orange>此符號代表 「兩個空格才換下個值」</font>. 例如 : INPUT name \$ & sex \$ age ...

同上, 輸入 :

```SAS
mei  f  .  68  100 /* 變成要兩個空格, 若只有一個會被當成字串 */
```

### 欄位輸入法(Column Input)

例 : INPUT initial \$ 1 name \$ 1-10 sex \$ 11 age 13-14 height 16-17 weight 19-21;

輸入 :

```SAS
Mei       f 14 68 100 /* 必須要在對應的位置上, 且長度有限制. 給予的對應位置可以重疊, 例如若有一個要身分證區號, 一個要全部身分證, 則可以 1 1-10 */
```

* 和簡列輸入法不同的地方在於, 有給定位置需要放甚麼(簡列只要以空白分隔就好, 這個有嚴格限制位置).
* 變項值可以用任何順序來讀取
* 數據可被重複讀取

### 格式輸入法(Formatted Input)

詳細範例看第 4-15 ~ 4-17 頁

| 符號        | 意義           | 範例  |
| ------------- |:-------------:| -----:|
| @n      | 跳到第 n 行(col) | @1 |
| +n      | 略過 n 行      | +3   |
| #n | 跳到下第n筆      | #3    |
| /  | 略過至下一筆 | / age |
| w. | 數值變項長度 | age 2. |
| w.d | 數值變相長度及小數點位數 | height 3.1|
| $ w. | 文字變相長度 | name $ 20.|
| Ew.d | 以科學記號儲存的數值變相長度 | income E10.|
| BZw.d | 將數值便項中的空白變成0 | income BZ10.2|
| COMMAw.d | 將所有數值中所含的符號清除, 並且以負號「-」來取代左括號| income COMMA10.2|
| DATEw. | 從 1960年1月1日起至所讀入日期的日數|days DATE9. [讀入日期為31DEC2009變為18262]|
|MMDDYYw. | 從1960年1月1日起至所讀入日期的日數, 日期以mmddyy或mmddyyyy 讀入 | days MMDDYY10. [12/31/2009變為18262]|
|DDMMYYW. | 從1960年1月1日起至所讀入日期的日數, 日期以ddmmyy或ddmmyyyy 讀入 | days DDMMYY10. [31/12/2009變為18262]|
|TIMEw. | 從午夜起至所讀入時間的秒數, 時間以 h:m:s格式表示 | seconds TIME8. [13:05:00變為47100]|
|DATETIMEw. | 從1960年1月1日起至所讀入時間的秒數, 時間以 ddmmyy/h:m:s格式表示 | seconds TIME8. [01JAN2010/00:00:00變為1577923200]|

可以將三種讀入法混用, 如下:

```SAS
DATA TEST1;
INPUT #1 name $ 10. /@1 price E7. #3 profit 6.2;
datalines;
Alice
123E2             1
50078
      Bob
 9.99E5
-30000
Anna Marie

-10000
;
run;
```

## 4.5 建構 SAS 資料檔的其他方法

### 將多筆紀錄整合成一個觀測體

對於單一個使用 row 輸入的資料, 可以使用 "/" 跳到下一行輸入; "#n" 跳到下 n row 的第一個 col; 使用多個 INPUT 來讀取.

```SAS
INPUT #3 balance 6.2 #2 account $ 1-7 #1 fname $ lname $;
```

```SAS
INPUT fname $lname $;
INPUT account $ 1-7;
INPUT balance 6.2;
```

### 從單一紀錄中讀取多個觀測體

若需要在同一個紀錄中輸入多個觀測體的資料, 可以在 INPUT 後面使用 @@ 分離.

```SAS
DATA double;
    INPUT group $ score @@;
DATALINES;
1 67 1 87 1 98
2 23 2 51 2 77
3 43 3 61 3 19
;
```

![4-5-1](圖片\4-5-1.PNG)

### 依據記錄之內涵區分資料

若要一次輸入擁有不同資訊的資料(例如社區和社區居明兩種), 可以依據他們的類型決定要哪個 INPUT.

例如以下依據不同 type, 去選擇要使用哪個 INPUT, 這裡的 INPUT

```SAS
DATA single;
    INPUT type $ 1. name $ 3-12 @; /*這裡可以輸入共通有的項目, 因為後面要依據 type 輸入至不同的 col, 所以要使用 @ 進行暫存. */
    IF type = 'c'
        THEN INPUT pop COMMA10. household COMMA10.;
    ELSE IF type = 'r'
        THEN INPUT income 10. tax 10.1;
DATALINES;
c Tainan     1,108,000 405,200
c Taipei     3,015,340 800,000
r Tainan         39000 3520
r Taipei         65000 20000
;
```

![4-5-2](圖片\4-5-2.PNG)

### 數個變項共用相同的輸入格式

若問題 a1-a10 和 b1-b10 都是填寫 1-5, q20a 和 q20b 都是輸入文字, 則可以使用以下輸入法.

```SAS
INPUT (a1-a10 b1-b10) (1.) (q20a q20b) ($);
```

若是其他循環輸入法, 例如 x1 和 x3 相同輸入; x2 和 x4 相同輸入, 則可參考以下寫法.

此寫法順序是依照 () 內的格式進行, 其中 +n 是空幾格.

```SAS
INPUT (x1-x4) (+2 3. +3 $5.) (x5-x10) (2. +1);
```

### MISSOVER

MISSOVER 用來阻止編譯器找不到值時使用下個值取代, 而是使用遺漏值.

```SAS
DATA miss;
    INFILE DATALINES MISSOVER;
    INPUT name $ item1 - item5;
DATALINES;
Alice 1 2 3
Bob   5 5 5 5 5
Cindy 1 1 1 1 1
;
```

若資料是以其他檔案儲存, 可使用以下輸入法.

```SAS
DATA miss;
    INFILE 'data path' MISSOVER;
    INPUT name $ item1 - item5;
RUN;
```

![4-5-3](圖片\4-5-3.PNG)

### TRUNCOVER

用途和 MISSOVER 相同, 結果也相同. 以下 code 也和 MISSOVER 相似.

```SAS
DATA miss;
    INFILE DATALINES TRUNCOVER;
    INPUT name $ 1-7 @8 (item1 - item5) (2.);
DATALINES;
Alice   1 2 3
Bob     5 5 5 5 5
Cindy   1 1 1 1 1
;
```

```SAS
DATA miss;
    INFILE 'data path' TRUNCOVER;
    INPUT name $ 1-7 @8 (item1 - item5) (2.);
RUN;
```

### 永久性儲存資料

若沒設置儲存點, 所有的 input data 都是存在暫存資料夾中(work). 可以使用 LIBNAME 選擇電腦中的資料夾, 用來讀取或儲存資料.

```SAS
LIBNAME research 'file path'; /* 這邊的 research 是給定名稱用來呼叫資料夾的, 可隨便設 */
DATA research.eval; /* 建立的資料放在 research 中, 叫做 eval. */
    INFILE 'data path';
    INPUT id $ a1-a5 b1-b5 q16-q19 q20a $;
RUN;
```

設定的資料夾以設定的名稱儲存於資料館中, 其中當關閉 SAS 時此資料館也會消失(但是儲存的資料會存在電腦中). 另外儲存的副檔名和版本有關, .sas7bat 代表 SAS 系統為 7、8 或 9版.

![4-5-4](圖片\4-5-4.PNG)

若要呼叫自己設定的資料館內的資料, 除了必須使用 LIBNAME 設定資料館外, 在呼叫的時候還必須使用 {Library_name}.{data_name} 呼叫.

# 5. 加強 SAS 程式與報表之可讀性

## 5.1 程式的文件化(註解或縮排)

有五種方式能在 code 中寫註解

1. 使用 /* */ 符號

```SAS
/* ABC */
```

2. 註解列 : 以 * 開頭, 以 ; 結尾

```SAS
* --------- *
|    ABC    |
* --------- *;
```

3. 空白列 : SAS 接受整段空白, 可利用此編程以利閱讀.

4. 縮排 : SAS 接受整段縮排, 可利用此編程以利閱讀.

5. TITLE

輸出報表時, 可以編寫TITLE, 用於顯示在 SAS output 視窗中. 長度包含空白最好在 60 位元以內.

第一個 TITLE 可以取 TITLE 或 TITLE1, 在同一個項目中之後的標題可以依序取 TITLE2 至 TITLE10.

```SAS
TITLE 'This is an illustration of the TITLE statement';

DATA Research.miss;
    INFILE DATALINES TRUNCOVER;
    INPUT name $ 1-7 @8 (item1 - item5) (2.);
DATALINES;
Alice   1 2 3
Bob     5 5 5 5 5
Cindy   1 1 1 1 1
;


TITLE2 'Results of PROC MEANS';

PROC MEANS DATA = Research.miss;
	VAR item1 - item3 item5;
run;
```

![5-1-1](圖片\5-1-1.PNG)

## 5-3 利用 LABEL 指令將變項標名

SAS 設置變項名稱通常只能 8 位元, 因此時常無法藉由變項名稱去了解變項涵義. 此時可以設置 LABEL 來詳述變項的涵義, 簡單來說類似新增一個用來解釋 col 的變項.

```SAS
TITLE 'This is an illustration of the TITLE statement';

DATA Research.miss;
    INFILE DATALINES TRUNCOVER;
    INPUT name $ 1-7 @8 (item1 - item5) (2.);
    LABEL name = "總之就是名稱"
          item1 = "看你想輸入什麼都可以"
          item2 = "可以是英文"
          item3 = "也可以是中文"
          item4 = "不過"
          item5 = "輸入中文可能英文版不能執行";
DATALINES;
Alice   1 2 3
Bob     5 5 5 5 5
Cindy   1 1 1 1 1
;


TITLE2 'Results of PROC MEANS';

PROC MEANS DATA = Research.miss;
	VAR item1 - item3 item5;
run;
```

![5-3-1](圖片\5-3-1.PNG)

## 5-4 利用 PROC FORMAT 和 FORMAT 將變項值標名

以 FREQ 生成的列聯表為例, 有時資料會將變數以數值或縮寫的字元表示, 此時若直接生成表, 會造成閱讀上的困難.

所以必須使用 PROC FORMAT 生成格式代名後再使用 FORMAT 將顯示的變項標名.

### 注意事項

1. 格式代名的目標變項也有區分為文字或數值, 若要使用 PROC FORMAT 生成文字格式的代名, 生成項目之名稱前也必須加上$. (例如 : $gender)
2. FORMAT 要放在 INPUT 之後
3. 使用 FORMAT 連結時, 變項要放在格式代名前. (例 : sex $gender)
4. 格式代名後須加上句點(.)
5. 下面 例子2 是生成 roster 進行永久性標明. 如果不想永久標明, 就不要再生成 roster 時進行標名.

### 若直接使用 FREQ 生成列聯表

```SAS
DATA roster;
     INFILE 'file_path';
     INPUT name $ sex $ id $ stand pretest first second final;

PROC FREQ DATA=roster;
     TABLES sex*stand ;
RUN;
```

![5-4-1](圖片\5-4-1.PNG)

### 以 PROC FORMAT 生成格式代名後使用 FORMAT 將變相名和格式代名連結 (永久標名)

```SAS
OPTIONS LS=82 PAGENO=1 NODATE;

TITLE 'This is an illustration of PROC FORMAT and FORMAT statements';

PROC FORMAT;
     VALUE $gender 'f'='female' 'm'='male';
     VALUE status 1='freshman' 2='sophomore' 3='junior' 4='senior';
RUN;

DATA roster;
     INFILE 'file_path';
     INPUT name $ sex $ id $ stand pretest first second final;
     FORMAT sex $gender.
            stand status.; /* 對 sex 和 stand 進行格式代名 */

TITLE2 'Results of PROC FREQ';
PROC FREQ DATA=roster;
     TABLES sex*stand / FORMAT=BEST10.;
RUN;
```

![5-4-2](圖片\5-4-2.PNG)

### 以 PROC FORMAT 生成格式代名後使用 FORMAT 將變相名和格式代名連結 (暫時標名)

```SAS
OPTIONS LS=82 PAGENO=1 NODATE;

TITLE 'This is an illustration of PROC FORMAT and FORMAT statements';

PROC FORMAT;
     VALUE $gender 'f'='female' 'm'='male';
     VALUE status 1='freshman' 2='sophomore' 3='junior' 4='senior';
RUN;

DATA roster;
     INFILE 'file_path';
     INPUT name $ sex $ id $ stand pretest first second final;

TITLE2 'Results of PROC FREQ';
PROC FREQ DATA=roster;
     TABLES sex*stand / FORMAT=BEST10.;
     FORMAT sex $gender.
            stand status.;
RUN;
```

結果同上.

## 5-5 利用 OPTIONS 指令控制 SAS 輸出報表的格式

OPTIONS 可以設定 SAS 的輸出格式, 可以參考上面的 code 第一行.

### OPTIONS 選項串

| 語法            | 解釋           |
| --------------- |:-------------:|
| CENTER          | 要求置中, 為內建值 |
| NOCENTER        | 要求靠左  |
| DATE            | 要求印出執行日期與時間, 為內建值   |
| NODATE          | 不列印時間和日期  |
| LABLE           | 要求執行變項標示, 為內建值  |
| NOLABEL         | 刪除對變項名稱的標名  |
| LINESIZE(或 LS) | 界定輸出報表每行的欄數, 須為 64 - 256 之間  |
| PAGENO          | 界定起始頁碼, 通常為1 |
| PAGESIZE(或 PS) | 界定報表每頁的列印數, 須為 15 - 32767 之間  |
