<!-- markdownlint-disable MD033 -->
<!-- markdownlint-disable MD010 -->
<!-- markdownlint-disable MD037 -->

# 流行病學方法

## 常見流行病學方法

1. 橫斷面研究(cross-sectional study)
2. 世代研究(cohort study)
3. 病例對照研究(case-control study)

Cohort study (Exposure) : 有使用藥物者
Case-control study(Outcome) : 在流行病學中, 代表曾經暴露過甚麼因素之後得到 Outcome 者為 D+.

![流行病學方法1](圖片\流行病學方法\1.PNG)

## 流行病學方法

健保資料庫的研究基本上都是 Retrospective cohort study (回溯性世代研究)

index date : 指的是開始暴露的時間點(實驗擷取開始點), 換句話說下圖下面第三點(排除index date 前, 有用藥的病患) 代表的是要取得首次使用抗精神病藥物的人.

此圖的實驗流程是該次實驗的流程, 並非所有實驗會是此流程. 此圖的實驗是 time to event (隨時間到事件發生(outcome))

time to event : 事件發生有無

![流行病學方法2](圖片\流行病學方法\2.PNG)

干擾因子 : 共病症、藥病史

處理此 cohort study 時, 處理健保資料庫有三個步驟要做

1. 把過去暴露過某個藥物的人擷取出來
2. 把 outcome(結果) 擷取出來
3. 針對干擾因子再處理

cohort stydy 的研究不一定是 time to event, 也可能目的是一年以後的血糖變化. 主要是因為流病的資料基本上是類別型, 對於此資料 time to event 是最常見的.

time to event 的處理方法最常見的是 cox proportional hazards model

藥物流行病學對於 outcome 的定義很講究, 暴露的定義方式有很多種.

其中一種暴露的定義例如 : 開始使用 antipsychotics, 若想知道使用此藥物的風險

1. Intent-to-treat(ITT) : 開始接受抗精神病藥物後, 看一年後是否有發生 event. 此觀察方式會有兩種可能, 分別是 event(發生事件) 和 Censor(設限, 代表無法觀測其事件發生的正確時間點, 例如死亡、消失或是觀察結束)
2. As treated : 實際上去考慮這群人這段期間領了多少藥, 利用領藥量來判斷這群人使用多久. 其中有吃完到下次領藥的空窗期. 結果有 event 和 Censor 兩種, 其中 Censor 有以下幾個
    1. Discontinuation : 停藥去換吃別的藥
    2. Switching : 換吃別的相同效果的藥物
    3. 合併另外一個藥
    4. Death
    5. End of continuous eligibility : 不再保健保(此研究以健保資料庫為主, 所以才有這項.)
    6. End

會有 As treated 主要因為可能會有停藥或是一些原因無法使用藥物等, 考量的因素太多所以才有這設計.

此研究有個問題在於是否為停藥, 因為有可能沒辦法領藥導致下次領藥的時間間格太長, 所以會<font size = 3 color = red>設 Grace period 來去判斷是否為停藥.</font>

臨床試驗較常使用 ITT, 主要是因為 ITT 得出的結果相對保守. 而 As treated 因為有太多 Censor, 所以就結果來說 ITT 的人年數會相對高點.

使用 ITT 或 As treated 沒有哪個是正確的, 可以根據當時疾病或是用藥狀況(時間需求長短)等去做選擇. 目前研究基本上是兩個都做, 但是對於目標期間是五年後之類的較長期間的研究, 使用 As treated 較為主流.

例如使用抗精神病藥後發生中風的風險, 此族群是所有使用抗精神病藥的人,就年齡來說相對老年, 並且有些藥物所使用的時間需求較長. 此研究是用 As treated.


## 實際例子

### 先取出/併入 stroke 日期

提取有 stroke(中風) 的資料

```SAS
/**stroke**/

proc sort data=selectpt; by id;run;
proc sort data=c.ipd_95; by id; run;

data stroke_95; /* 雖然目標是96~97, 不過看95也可以幫code 檢查 bug. */
	merge selectpt(in=x) c.ipd_95 (in=y);
	by id;
	if x and y;
    pre_date=input(in_date, yymmdd10.);/* 把文字格式的日期轉換成數值(從某日開始以天數計算), 後面要有 format 才能轉換成日期格式 */
	if index_date <= pre_date <= index_date + 365; /* 藥物使用後的一年內有發生的才算. 此條件看研究定義, 可能覺得開藥當天發生 outcome 不合理所以不能有 index_date = pre_date. */
	if  "430"<=substr(ICD9CM_1,1,3)<="438" then stroke=1; /* 430~438 : 中風 */
	if  "430"<=substr(ICD9CM_2,1,3)<="438" then stroke=1; /* 雖然是 ICD9CM 是 str, 不過一樣可以使用 <= 來判斷是否在 430~438 間 */
	if  "430"<=substr(ICD9CM_3,1,3)<="438" then stroke=1; /* 如果不想寫那麼多 if then, 可以使用 or */
	if  "430"<=substr(ICD9CM_4,1,3)<="438" then stroke=1;
	if  "430"<=substr(ICD9CM_5,1,3)<="438" then stroke=1;
	if stroke=1; /* 只列出有發生 stroke 的*/
	stroke_date = pre_date; /* 已經確定發生 stroke, 則發病日期就是就醫日期 */
	format pre_date stroke_date yymmdd10.; /* 將數值日期改格式 */
	keep id stroke stroke_date;
run;
```

使用 macro 去做 95~98

```SAS
%macro stroke;
%do i=95 %to 98;
proc sort data=selectpt; by id;run;
proc sort data=c.ipd_&i.; by id; run;
data stroke_&i.;
	merge selectpt(in=x) c.ipd_&i. (in=y);
	by id;
	if x and y;
    pre_date=input(in_date, yymmdd10.);
	if index_date<=pre_date<=index_date+365;
	if  "430"<=substr(ICD9CM_1,1,3)<="438" then stroke=1;
	if  "430"<=substr(ICD9CM_2,1,3)<="438" then stroke=1;
	if  "430"<=substr(ICD9CM_3,1,3)<="438" then stroke=1;
	if  "430"<=substr(ICD9CM_4,1,3)<="438" then stroke=1;
	if  "430"<=substr(ICD9CM_5,1,3)<="438" then stroke=1;
	if stroke=1;
	stroke_date=pre_date;
	format pre_date stroke_date yymmdd10.;
	keep id stroke stroke_date icd9cm_1-icd9cm_5 index_date;
run;
%end;
%mend;
%stroke;
```

合併

```SAS
data stroke;
	set stroke_95-stroke_98;
run;
```

目標雖是發生 stroke, 不過可能因為住院多次造成同個人被抓出很多次, 所以目標是只有第一次發生事件的資料而已.

```SAS
proc sort data=stroke; by id stroke_date;run; /* 依照 id, 發生日期排序(小到大) */
proc sort data=stroke nodupkey; by id; run; /* nodupkey : 只取第一筆(後面重複的不要紀錄) */
```

上面的也可以使用 SQL, 因為如果那樣打的話可能會因為重複搜尋導致耗費時間較長. 以下 SQL 語法不一定比較快(沒比較過)

```SAS
PROC SQL;
CREATE TABLE 
    new_table_name 
AS
SELECT DISTINCT /* DISTINCT : 不重複 , 其實不加上這個最後結果也只會有一個.*/
    feature1, MIN(feature2) AS feature2 /* feature1 和 2 分別代表 id 和 stroke_date */
FROM
    your_table
GROUP BY /* 依照 feature1 分組 */
    feature1
ORDER BY /* 分組後排序 */
    feature1;
quit; 
/* 整個 code 簡單來說就是分組後排序 feature1, 然後取
```

合併原始資料和 stroke 日期.

```SAS
data b.master3;
	merge b.master2 (in=x) stroke (in=y keep=id stroke stroke_date);
	by id ;
	if x ;
	if stroke=. then stroke=0;
run;
```

censor : discontinuation(停藥), switching(換藥)

因為此研究目標是比較第一代和第二代的藥, 所以若 switching 是第二代換成另外一個第二代的藥則認為不是問題; 若是一換二或二換一則是 switching.

當然也可能先停藥後換藥(或相反), 不過一定會有個日期先出現, 這時就看研究定義再決定怎麼做.

### 針對 censor 處理

#### death

death 可以用死因檔處理, 相對簡單.

```SAS
/***death***/

proc sort data=a.H_ost_death95; by id; run;
data death_95;
	merge selectpt(in=x) a.H_ost_death95 (in=y keep =id d_date);
	by id;
	if x;
	death_date=input(d_date, yymmdd10.);
	if index_date<=death_date<=index_date+365;
	death=1;
	format death_date yymmdd10.;
run;
```

轉換成 %macro.

```SAS
%macro de;
%do i= 95 %to 98;
proc sort data=a.H_ost_death&i.; by id; run;
data death_&i.;
	merge selectpt(in=x) a.H_ost_death&i. (in=y keep =id d_date);
	by id;
	if x;
	death_date=input(d_date, yymmdd10.);
	if index_date<=death_date<=index_date+365;
	death=1;
	format death_date yymmdd10.;
run;
%end;
%mend;
%de;
```

合併檔案

```SAS
data death;
	set death_95-death_98;
run;
```

理論上死亡次數每人只有一筆, 不過可能輸入錯誤或其他問題造成不只一筆. 這時可以看就醫紀錄等看中間是否有就醫, 若有則是後面那筆才可能是真的.

```SAS
proc sort data=death; by id descending death_date;run; /* 假設後面那筆死亡記錄才是正確的 */
proc sort data=death nodupkey; by id; run;

proc sql; /* 合併檔案 */
create table b.master4 as select
a.*, b.death, death_date 
from b.master3 a
left join death b on a.id=b.id
;
quit;
```

#### exposure : discontinuation

discontinuation  gap=30, gap 代表是否停藥的判斷時間間格. 依照研究自行定義時間, 此為 30 天. 基本上定義的方法會去看 data base 和藥品的狀況.

如果停藥的話, 停藥的日期為最後用藥日(有的研究會加上 gap 的時間).

通常 discontinuation 不會檢閱住院檔, 只會檢閱門診檔與藥據. 原因是因為住院檔無法得知藥品是甚麼時候開始使用.

以下 code 為取有使用精神病藥的人, 並且因為要計算是否在 gap 內, 所以要取 drug_day.

註 : 過去資料的 drug_day 會登入時都會登記當次開立藥物最長時間.

```SAS
%macro drug;
%do i= 95 %to 98;
proc sql; 
create table psy_&i. as select
a.id, a.func_date, a.drug_day, b.index_class as pre_class, b.index_agent as pre_agent /* 因為 a 和 b 都有相同的 feature(但是值不一定相同), 所以要使用 as 重新命名. */
from c.opd_&i. a 
inner join c.n05a b on a.drug_no = b.drug_code
;
quit; 
%end;
%mend;
%drug;
```

合併檔案

```SAS
data psy_drug;
set psy_95-psy_98;
pre_date=input(func_date, yymmdd10.);
format pre_date yymmdd10.;
drop func_date;
run;
```

keep 資料另外儲存

```SAS
data selectpt  (keep=id index_date index_class) ;
	set b.master4;
run;
```

```SAS
proc sort data=psy_drug; by id; run;
proc sort data=selectpt; by id; run;

data dur;
	merge selectpt(in=x) psy_drug (in=y);
	by id;
	if x;
	if index_date<=pre_date<=index_date+365;
run;
```

此時, 只有一筆資料的, 用結束日就是用較結束日(pre_date); 若有人有很多筆資料, 則計算各兩組的中間訊息. 所以兩個要分開處理.

新創兩個 col (first, last), 在 id 跟 pre_date 排序後的結果, 最前面的在 first 補 1; 最後面的 lst 補1. 若有個 id 的 first 跟 last 在同一行都有 1, 則那個 id 就只有一筆用藥紀錄.

```SAS
proc sort data=dur; by id pre_date; run;

data dur1 dur2;
	set dur;
	by id;
	if first.id then first=1;
	if last.id then last=1;
	if first+last=2 then output dur1; else output dur2; /* 如果只有一筆資料放到 dur1, 有多筆資料的放到 dur2 */
```

針對只有一筆資料的資料處理, 找出用藥結束時間.

```SAS
/***patients with only one prescription***/
data dur3; 
	set dur1;
	discon_date = pre_date + drug_day; /* 結束日是領藥日+用藥日, 註 : 因為只有一筆, 所以 診斷日(index_date) 和 領藥日 相同天 */
	discon=1; /* 之後就停藥了 */
	format discon_date yymmdd10.;
	keep id discon discon_date;
run;
```

處理多筆資料的人, gap day 為前一次的 領藥日+用藥日 和 該次的 領藥日 的差額.

lag function : 把資料往後移

<font size = 4 color = red> 問題, if gap .. </font>

```SAS
/***patients with multiple prescription***/

data dur4;
	set dur2;
	pre_end_date = pre_date + drug_day;
	pre_lag = lag(pre_end_date);
	gap = pre_date-pre_lag; /* 計算前一筆的 領藥日+用藥日 和後一筆的 領藥日 時間差 */

	if first = 1 then do pre_lag =.; gap=.; end; /* 第一筆資料並不需要和前一筆相減 */
	if gap > 30 then do discon=1; discon_date = pre_lag;end; /* 為什麼最後一筆 last.id 不算 discon = 1? 前面只有一筆時, 都把他們都做 discon = 1 */

	format pre_end_date pre_lag discon_date yymmdd10.;

	if discon=1;
	keep id discon discon_date;
run;
```

```SAS
proc sort data=dur4; by id discon_date; run;
proc sort data=dur4 nodupkey out=dur5; by id; run;

data dur6;
	set dur3 dur5;
run;
```

#### switching

第一代和第二代互換的話有沒有差異

```SAS
/***switching***/

proc sort data=psy_drug; by id; run;
proc sort data=selectpt; by id; run;

data switch;
	merge selectpt (in=x) psy_drug (in=y);
	by id;
	if x and y;
	if index_date<pre_date<=index_date+365;
	if index_class ^= pre_class then do ;switching=1;switching_date=pre_date;end;
	format switching_date yymmdd10.;
	if switching=1;
	keep id switching switching_date;
run;
```

```SAS
proc sort data=switch; by id switching_date;run;
proc sort data=switch nodupkey; by id; run;

proc sql;
create table b.master6 as select
a.*, b.switching, b.switching_date
from b.master5 a
left join switch b on a.id=b.id
;
quit;
```

#### eligibility

檢視是否在健保期間內

```SAS
/*continuous eligibility and last date and 1 year followup*/
data b.master7;
	set b.master6;
	if out_date<index_date+365 then do; disenroll=1; disenroll_date=out_date;end;
	
	follow1yr=1;
	follow1yr_date=index_date+365; /* 觀察一年後的日期 */
	
	last_date='31dec2009'd; /* 資料庫的最後一天 */
	last_flag=1;

	format disenroll_date follow1yr_date last_date yymmdd10.;
run;
```

#### 出現哪個 outcome

因為每個 outcome(死亡、停藥等) 都有日期, 所以找最小的日期那個就好. 若都在同一天, 則在 if 中先寫的會成為他的 outcome.

```SAS
/*outcome determination*/

data b.master8;
set b.master7;

follow_end=min (discon_date,switching_date, death_date,
follow1yr_date,last_date, disenroll_date,stroke_date ); /* 找最小的日期 */

if follow_end=stroke_date then event='stroke           '; /* 因為文字長度會依照第一個定義給的文字長度作為此欄位的長度, 所以要加空白來增加長度, 或使用 Length */
else if follow_end=switching_date  then event='switch' ;
else if follow_end=discon_date  then event='discon' ;
else if follow_end=death_date  then event='death' ;
else if follow_end=disenroll_date  then event='disenroll' ;
else if follow_end=follow1yr_date  then event='follow1yr' ;
else event='lastday' ; /* 出現哪個時間最小, 就是哪個 outcome. 如果都沒有, 就是 lastday */

if event='stroke' then event_flag=1; else event_flag=0;

FOLLOW_DAY=follow_end-index_date; /* 如果要畫 survival curve 必須要有存活時間 */

format follow_end yymmdd10.; 
run;
```

計算 2 by 2 table

```SAS
proc freq data=b.master8  ;
table index_class*event_flag / nocol norow nopercent; /* 如果不加 nocol norow nopercent 會把所有百分比都算出來 */
run;
```

### covariates

共病症(comorbidity) - 從住院跟門診檔抓

```SAS
/*covariates*/

data Selectpt (keep=id index_date);
set b.master8;
run;
```

共病症要抓取 門診的 ICD9/10

```SAS
%macro dx;
%do i=95 %to 98;
proc sql;
create table opddx_&i. as select 
a.*, b.icd9cm_1, b.icd9cm_2, b.icd9cm_3, input(b.func_date, yymmdd10.) as pre_date format yymmdd10., b.atc_7
from selectpt a
inner join c.opd_&i. b on a.id=b.id
having index_date-180<pre_date<= index_date
;
quit;
%end;
%mend;
%dx;
```

看他有哪些疾病和用哪些藥物.

```SAS
%macro dx_1;
%do i=95 %to 98;
proc sql;
create table ipddx_&i. as select 
a.*, b.icd9cm_1, b.icd9cm_2, b.icd9cm_3, b.icd9cm_4, b.icd9cm_5,
input(b.in_date, yymmdd10.) as pre_date format yymmdd10., b.atc_7
from selectpt a
inner join c.ipd_&i. b on a.id=b.id
having index_date-180<pre_date<= index_date
;
quit;
%end;
%mend;
%dx_1;
```

```SAS
data baseline;
set 
opddx_95-opddx_98
ipddx_95-ipddx_98;
run;
```


```SAS

data baseline2;
set baseline;

/* 設定共病症 */
Schizophrenia=0;
Depression=0;
DM=0;
Dyslipidemia=0;
IHD=0;
HTN=0;
COPD=0;

array discode{5} icd9cm_1-icd9cm_5; /* 使用方法參考下圖 */
do i=1 to DIM(discode); /* DIM 是 array 的函數之一, 用來獲取維度或元素的個數. 另外之後 table 會產生名為 i 的 col, 無視即可, 主要是因為有 "i =", SAS 把它當成一個變數的命名 */

/* 共病症 : comorbidity */
if (substr(left(discode{i}),1,3)='295') then Schizophrenia=1;
if (substr(left(discode{i}),1,3)='311') then Depression=1;
if (substr(left(discode{i}),1,3)='250') then DM=1;
if (substr(left(discode{i}),1,3)='272') then Dyslipidemia=1;
if (substr(left(discode{i}),1,3) in ('410','411','412','413','414')) then IHD=1;
if (substr(left(discode{i}),1,3) in ('401','402','403','404','405')) then HTN=1;
if (substr(left(discode{i}),1,3) in ('491','492')) then COPD=1;
end; 


if substr(atc_7,1,3)='A10' then antidiabete=1;else antidiabete=0;
if substr(atc_7,1,5)='C10AA'  then  Statin=1   ;   else Statin=0   ;  
if substr(atc_7,1,3)='C03' then diuretic=1;else diuretic=0;
if substr(atc_7,1,3)='C07' then betablocker=1 ;else betablocker=0;
if substr(atc_7,1,3)='C08' then CCB=1; else CCB=0; 

run;
```

因為有些人有多個就醫紀錄, 所以有時會有有時不會有. 此時只要留下有的那筆即可. 以下為把所有id, 若有重複則變成一筆. 並且其他時間點若有發生共病症等, 則在那個 col 標示成1.

```SAS
proc means data=Baseline2 nway noprint; class id; /* 找相同 id 的以下幾個 col, 找最大值(只有0跟1) */
var  /* colname為以下幾個 */
Schizophrenia
Depression
DM
Dyslipidemia
IHD
HTN
COPD
antidiabete
Statin
diuretic
betablocker
CCB;
output out=baseline3

Max(Schizophrenia)=Schizophrenia
Max(Depression)=Depression
Max(DM)=DM
Max(Dyslipidemia)=Dyslipidemia
Max(IHD)=IHD
Max(HTN)=HTN
Max(COPD)=COPD
Max(antidiabete)=antidiabete
Max(Statin)=Statin
Max(diuretic)=diuretic
Max(betablocker)=betablocker
Max(CCB)=CCB
;
run;
```

合併檔案

```SAS
proc sort data=Baseline3; by id;run;
proc sort data=b.master8; by id;run;

data b.master9;
merge b.master8 (in=x) Baseline3;
by id ; 
if x ;
drop _type_ _freq_;
run;
```

其他分析條件

```SAS
data and;
set master9;
if age_50 = 1 
and eligibility = 1
and pastuser = 0;
run;
```

## 補充

### array 使用方法

給定數據和方法, 自動產生各個方法的值. 

可以使用 
```SAS
array array_name{} feature(s);
```
來對 feature(s) 建立一個暫存的 array, 此 array 可以使用 {} 來去呼叫feature, 另外對於 array 的更改不會直接影響到原本的 features 上.

![array](圖片\流行病學方法\3.PNG)

![array2](圖片\流行病學方法\4.PNG)

#### array 相關函數

```SAS
DIM(array_name); /* 用來檢視 array 的 維度 */
```

```SAS
cll missing(of array_name{*}); /* 將數組中的元素設置為缺失值 */
```

```SAS
sum = sum( of array_name{*}); /* of operator : 對於數組中多個變數執行相同的操作 */
```

### 其他指令

```SAS
trim(str); /* 移除前後的空白 */
```

```SAS
length(); /* 計算文字長度 */
```